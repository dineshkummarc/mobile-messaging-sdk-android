buildscript {

    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0'
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'maven-publish'
}

ext {
    mm_minSdkVersion = 14
    mm_compileSdkVersion = 28
    mm_targetSdkVersion = 28
    mm_buildToolsVersion = "28.0.3"
    mm_supportLibrariesVersion = "28.0.0"
    mm_firebaseMessagingVersion = "17.6.0"
    mm_gcmVersion = "16.1.0"
    mm_gmsLocationVersion = "16.0.0"
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

apply plugin: 'io.codearte.nexus-staging'

subprojects {
    apply plugin: "maven"
    apply plugin: "maven-publish"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersionInReadme {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-android-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-android-sdk:$version@aar")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme

// workaround for `net.researchgate.release` plugin issue:
// https://github.com/researchgate/gradle-release/issues/186
task build {}
build.dependsOn subprojects.findAll {!it.name.contains("demo")}.build

afterReleaseBuild.dependsOn subprojects.publish

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}